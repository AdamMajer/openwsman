dnl *******************************************
dnl *** Initialize automake and set version ***
dnl *******************************************

AC_PREREQ(2.53)
AC_INIT(openwsman, 0.3.2)
AC_CONFIG_SRCDIR(src/server/wsmand.c)
AM_INIT_AUTOMAKE(AC_PACKAGE_NAME, AC_PACKAGE_VERSION)

AM_CONFIG_HEADER(wsman_config.h)
AM_MAINTAINER_MODE
AC_PROG_MAKE_SET


dnl ***************************
dnl *** Set debugging flags ***
dnl ***************************

debug_default=minimum

# Declare --enable-* args and collect ac_help strings
AC_ARG_ENABLE(debug, 
	      [  --enable-debug=[no/minimum/yes] turn on debugging [default=$debug_default]],,
	      enable_debug=$debug_default)


AC_ARG_WITH(libsoup,
            [AC_HELP_STRING([--with-libsoup-serv],
                            [use libsoup for transport backend.])])
AC_ARG_WITH(shttpd,
            [AC_HELP_STRING([--with-shttpd],
                            [use builtin httpd server for transport backend.])])
with_libsoup_serv=yes
if  test "$with_shttpd" = yes; then
    AC_MSG_RESULT([Using built in transport backend])
   with_shttpd=yes
   with_libsoup_serv=no
fi 
dnl libsoup wins
if test "$with_shttpd" = "$with_libsoup_serv"; then
    AC_MSG_ERROR([Sorry, only one server transport backend can be selected. Use libsoup])
   with_libsoup_serv=yes
   with_shttpd=no
fi 
AM_CONDITIONAL(USE_LIBSOUP_SERV, test "$with_libsoup_serv" == "yes")
AM_CONDITIONAL(USE_SHTTPD, test "$with_shttpd" == "yes")


with_client=yes
without_client=no
AC_ARG_WITH(client,
            [AC_HELP_STRING([--with-client],
                            [compile client.])])


AC_ARG_WITH(libcurl,
            [AC_HELP_STRING([--with-libcurl],
                            [use libcurl for client transport backend.])])
AC_ARG_WITH(libsoup-client,
            [AC_HELP_STRING([--with-libsoup-client],
                            [use libsoup for client transport backend.])])
with_libsoup_client=yes
if  test "$with_libcurl" = yes; then
    AC_MSG_RESULT([Using built in transport backend])
   with_libcurl=yes
   with_libsoup_client=no
fi 
dnl libsoup wins
if test "$with_libcurl" = "$with_libsoup_client"; then
    AC_MSG_ERROR([Sorry, only one client transport backend can be selected. Use libsoup])
   with_libsoup_client=yes
   with_libcurl=no
fi 
AM_CONDITIONAL(USE_LIBSOUP_CLIENT, test "$with_libsoup_client" == "yes")
AM_CONDITIONAL(USE_LIBCURL, test "$with_libcurl" == "yes")
AM_CONDITIONAL(COMPILE_CLIENT, test "$without_client" == "no")

# Set the debug flags
if test "x$enable_debug" = "xyes"; then
	test "$cflags_set" = set || CFLAGS="$CFLAGS -g"
	OWSMAN_DEBUG_FLAGS="-DOWSMAN_ENABLE_DEBUG"
else
	if test "x$enable_debug" = "xno"; then
		OWSMAN_DEBUG_FLAGS="-DG_DISABLE_ASSERT -DG_DISABLE_CHECKS"
	fi
fi


# Set the debug flags
if test "x$enable_debug" = "xyes"; then
    AC_DEFINE(WSMAN_DEBUG_VERBOSE, 1, [Defined if verbose debug logging is requested])
fi
AC_SUBST(OWSMAN_DEBUG_FLAGS)

dnl
if test "x$with_libsoup_serv" = "xyes"; then
    AC_DEFINE(LIBSOUP_LISTENER, 1, [Defined if libsoup listener to be used])
else
    AC_DEFINE(SHTTPD_LISTENER, 1, [Defined if shttpd listener to be used])
fi
if test "x$with_libsoup_client" = "xyes"; then
    AC_DEFINE(LIBSOUP_CLIENT, 1, [Defined if libsoup to be used in client])
else
    AC_DEFINE(LIBCURL_CLIENT, 1, [Defined if libcurl to be used in client])
fi

PKG_CHECK_MODULES(GMODULE, gmodule-2.0)
PKG_CHECK_MODULES(GLIB, glib-2.0 >= 2.8.0 )
PKG_CHECK_MODULES(GOBJECT, gobject-2.0)
PKG_CHECK_MODULES(GTHREAD, gthread-2.0)
if test "x$with_libsoup_serv" = "xyes" -o "x$with_libsoup_client" = "xyes"; then
     PKG_CHECK_MODULES(SOUP, libsoup-2.2)
fi
if test "x$with_libcurl" = "xyes"; then
     PKG_CHECK_MODULES(CURL, libcurl)
fi

AC_CHECK_FUNCS(strtok_r \
getpid \
strsep \
gettimeofday \
unlink \
syslog \
sleep \
fnmatch \
timegm \
daemon 
)

# check for ssize_t
AC_CHECK_TYPE(ssize_t, int)

dnl ----------------------------------------------------------------------
AC_CHECK_HEADERS([unistd.h sys/types.h sys/sendfile.h sys/signal.h])
AC_CHECK_HEADERS([ctype.h sys/resource.h sys/socket.h sys/select.h])
AC_CHECK_HEADERS([netinet/in.h], [], [],
[#if HAVE_SYS_TYPES_H
# include <sys/types.h>
#endif
])

dnl Checks for types
AC_CHECK_TYPES([sa_family_t, in_port_t, in_addr_t], , ,
[
#if HAVE_SYS_TYPES_H
# include <sys/types.h>
#endif
#if HAVE_SYS_SOCKET_H
# include <sys/socket.h>
#endif
#if HAVE_NETINET_IN_H
# include <netinet/in.h>
#endif
#if HAVE_ARPA_NAMESER_H
# include <arpa/nameser.h>
#endif
])

dnl ***************************
dnl *** Checks for programs ***
dnl ***************************

AC_PROG_CC
AM_PROG_CC_STDC
AC_PROG_INSTALL

# Set STDC_HEADERS
AC_HEADER_STDC

# Initialize libtool
AC_LIBTOOL_WIN32_DLL
AM_PROG_LIBTOOL

# This isn't a program, but it doesn't fit anywhere else...
AC_FUNC_ALLOCA

PKG_CHECK_MODULES(XML, libxml-2.0)
AC_ARG_ENABLE(doxygen-docs,     [  --enable-doxygen-docs     build DOXYGEN documentation (requires Doxygen)],enable_doxygen_docs=$enableval,enable_doxygen_docs=auto)

dnl ***********************
dnl *** Check for Win32 ***
dnl ***********************

AC_MSG_CHECKING([for Win32])
case "$host" in
  *-*-mingw*)
    os_win32=yes
    AC_CACHE_VAL(ac_cv_func_getaddrinfo, [ac_cv_func_getaddrinfo=yes])
    AC_CACHE_VAL(ac_cv_func_getnameinfo, [ac_cv_func_getnameinfo=yes])
    AC_CACHE_VAL(ac_cv_func_inet_pton, [ac_cv_func_inet_pton=yes])
    AC_CACHE_VAL(ac_cv_func_inet_ntop, [ac_cv_func_inet_ntop=yes])
    AC_CACHE_VAL(soup_cv_ipv6, [soup_cv_ipv6=yes])
    ;;
  *)
    os_win32=no
    ;;
esac
AC_MSG_RESULT([$os_win32])
AM_CONDITIONAL(OS_WIN32, [test $os_win32 = yes])

dnl *******************
dnl *** Misc checks ***
dnl *******************
AC_CHECK_FUNCS(gmtime_r)

dnl *********************************
dnl *** Networking library checks ***
dnl *********************************

AC_CHECK_FUNC(socket, , AC_CHECK_LIB(socket, socket))
AC_CHECK_FUNC(gethostbyname, , AC_CHECK_LIB(nsl, gethostbyname))
AC_CHECK_FUNCS(inet_pton inet_ntop inet_aton getaddrinfo getnameinfo)
AC_CACHE_CHECK(IPv6 support, soup_cv_ipv6, [
	AC_EGREP_HEADER(sockaddr_in6, netinet/in.h, soup_cv_ipv6=yes, soup_cv_ipv6=no)
])


AC_ARG_ENABLE(ssl, 
              [  --enable-ssl             Turn on Secure Sockets Layer support [default=yes]],,
              enable_ssl=auto)

if test "$enable_ssl" != "no"; then
    AM_PATH_LIBGNUTLS(1.0.0, have_ssl=yes, have_ssl=no)

    if test "$have_ssl" != "yes"; then
        if test "$enable_ssl" = "auto"; then
            AC_MSG_WARN(Disabling SSL support);
            enable_ssl=no;
        else
            AC_MSG_ERROR(Could not configure SSL support);
        fi
    fi
fi

if test "$enable_ssl" != "no"; then
    AC_DEFINE(HAVE_SSL, 1, [Defined if you have SSL support])
fi


AC_DEFINE_UNQUOTED([DEFAULT_CONFIG_FILE], ["${sysconfdir}/openwsman/openwsman.conf"] , [Default config file])
SYSCONFDIR=${sysconfdir}/openwsman

dnl Subst PACKAGE_PLUGIN_DIR.
packageplugindir=$libdir/${PACKAGE}/plugins
PACKAGE_PLUGIN_DIR="${packageplugindir}"
AC_SUBST(PACKAGE_PLUGIN_DIR)

dnl *************************************
dnl *** Warnings to show if using GCC ***
dnl *************************************

AC_ARG_ENABLE(more-warnings,
	      [  --disable-more-warnings  Inhibit compiler warnings],
	      set_more_warnings=no)

if test "$GCC" = "yes" -a "$set_more_warnings" != "no"; then
	CFLAGS="$CFLAGS \
		-Wall -Wstrict-prototypes -Wmissing-declarations \
		-Wmissing-prototypes -Wnested-externs -Wpointer-arith \
                -Wunused"
fi


if test "$os_win32" != yes; then
	# Use reentrant functions (FIXME!)
	CFLAGS="$CFLAGS -D_REENTRANT"
fi


CHECK_SFCC
have_sfcc=no
AC_CHECK_LIB([cmpisfcc],  [cmciConnect], [have_sfcc=yes])

AC_PATH_PROG(DOXYGEN, doxygen, no)

AC_MSG_CHECKING([whether to build Doxygen documentation])

if test x$DOXYGEN = xno ; then
    have_doxygen=no
else
    have_doxygen=yes
fi

if test x$enable_doxygen_docs = xauto ; then
    if test x$have_doxygen = xno ; then
        enable_doxygen_docs=no
    else
        enable_doxygen_docs=yes
    fi
fi

if test x$enable_doxygen_docs = xyes; then
    if test x$have_doxygen = xno; then
    AC_MSG_ERROR([Building Doxygen docs explicitly required, but Doxygen not found])
    fi
fi

AM_CONDITIONAL(OWSMAN_DOXYGEN_DOCS_ENABLED, test x$enable_doxygen_docs = xyes)
AC_MSG_RESULT(yes)


###
### static?
###
#AC_ARG_ENABLE(static-binary,
#  [  --enable-static-binary  link as many libraries statically as possible])

##if test "$enable_static_binary" = "yes"; then
## LINK_BEGIN="-Wl,-Bstatic"
##  LINK_END="-Wl,-Bdynamic"
##else
##  LINK_BEGIN=
##  LINK_END=
##fi
##AC_SUBST(LINK_BEGIN)
##AC_SUBST(LINK_END)
##AC_SUBST(SHARED_ONLY)
AC_SUBST(ENABLED_PLUGINS)

dnl *************************
dnl *** Output Everything ***
dnl *************************
AC_SUBST(SYSCONFDIR)

AC_OUTPUT([
        Doxyfile
	Makefile
        src/Makefile
        src/server/Makefile
        src/client/Makefile
        src/lib/Makefile
        src/libcim/Makefile
        src/libcim/include/Makefile
        src/libcim/sfcc/Makefile
        src/plugins/Makefile
        src/plugins/cim/Makefile
        src/plugins/identify/Makefile
        src/libu/Makefile
        src/libu/u/Makefile
        doc/Makefile
        doc/specs/Makefile
        etc/Makefile
        openwsman.spec
        etc/owsmangencert.sh
        etc/init/Makefile
        etc/init/wsmand.sh
	])
